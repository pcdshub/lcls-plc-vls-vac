<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.14">
  <POU Name="PRG_VLS_WINDOW" Id="{06bf4e71-7665-4602-9a1f-3fc3cd0692b8}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM PRG_VLS_WINDOW
VAR
	xFirstPass		: BOOL;
	fbFSInit		: R_TRIG;
	
	state			: E_STATES;

	bOverride		: BOOL;
	bSetup			: BOOL;
	
	//GVL_Devices
	VGC_1			: FB_VGC;
	VGC_W			: FB_VRC_NO; // Is the Window valve NO 

	//US_GPI			: FB_MKS275;
	//US_GCC			: FB_MKS422;

	DS_GPI			: FB_MKS275;
	DS_GCC			: FB_MKS500;
END_VAR
VAR_INPUT
	bExtPress		: BOOL := FALSE; // Input for external interlock with hutch vacuum system
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[fbFSInit( CLK := TRUE, Q => xFirstPass);
(* On first PLC pass, put valve into init state*)
IF xFirstPass THEN
	state:= E_STATES.INIT;
	bOverride := FALSE;
	bSetup := FALSE;
END_IF


//STATE ACTIONS
CASE state OF

 E_STATES.INIT: 
		VGC_1.M_Set_OPN_SW(FALSE); //Isolation Valve closed
		VGC_W.M_Set_CLS_SW(FALSE); // Window valve OPEN
		VGC_1.i_xOverrideMode := VGC_W.i_xOverrideMode := FALSE;
		//Transition
		if (VGC_1.M_IsClosed() AND VGC_W.M_IsOpen()) THEN
			state:= E_STATES.NOT_SAFE;
		END_IF
		
E_STATES.NOT_SAFE: // System is not allowed to actuate, i.e all interlocks are in effect
 	VGC_1.i_xExt_OK := VGC_1.i_xEPS_OPN_OK := VGC_1.i_xEPS_CLS_OK:= FALSE; 
	VGC_W.i_xExtILK_OK:= FALSE; 
	VGC_1.i_xOverrideMode := VGC_W.i_xOverrideMode := FALSE;

	//Transition
	//IF (US_GCC.IG.xAT_VAC AND DS_GCC.IG.xAT_VAC) THEN state:= E_STATES.READY; END_IF
	IF (bExtPress AND DS_GCC.IG.xAT_VAC) THEN state:= E_STATES.READY; END_IF
	
	
	
 E_STATES.READY: // OK to MOVE
 	// Release the external interlock so the differential pressure inlk is in effect
 	VGC_1.i_xExt_OK:= VGC_1.i_xEPS_OPN_OK := VGC_1.i_xEPS_CLS_OK:= TRUE; 
	//permission to insert window valve only when US vacuum is good??
	VGC_W.i_xExtILK_OK:= bExtPress;
	// Valves  alternate positions.
	If (VGC_W.M_IsClosed()) THEN  VGC_1.M_Set_OPN_SW(TRUE);
	ELSIF (VGC_W.M_IsOpen()) THEN VGC_1.M_Set_OPN_SW(FALSE);
	END_IF
	
 
 E_STATES.OVERRIDE: 
	// Override Mode is enabled for all valves
 	VGC_1.i_xOverrideMode :=VGC_W.i_xOverrideMode := TRUE;
	// Exit of Override mode always goes back to init
 	IF NOT(bOverride) THEN state:= E_STATES.INIT; END_IF
	
	
 E_STATES.SETUP: 
	// Window valve remains retracted
 	VGC_W.i_xExtILK_OK := FALSE; 
	// Isolation valves operates like standard gate valves 
	VGC_1.i_xExt_OK := TRUE;
	// exit to init just to be safe?
	IF NOT(bSetup) THEN state:= E_STATES.INIT; END_IF
END_CASE


// STATE TRANSITIONS
// the system can go to setup state from any other state except override
IF (bSetup) AND (state<>E_STATES.OVERRIDE) THEN state:= E_STATES.SETUP; 
// the system can go to override state from any other state except setup
ELSIF (bOverride) AND (state<>E_STATES.SETUP) THEN state:= E_STATES.OVERRIDE;
//Loss of at vac signal makes the system go to init/not safe to move state
ELSIF (NOT bExtPress) OR (NOT DS_GCC.IG.xAT_VAC) THEN state:= E_STATES.INIT;
//ELSIF (NOT US_GCC.IG.xAT_VAC) OR (NOT DS_GCC.IG.xAT_VAC) THEN state:= E_STATES.INIT;
END_IF 


// Instantiate function blocks
//US_GPI(PG=>);
//US_GCC(PG:=US_GPI.PG);
DS_GPI(PG=>);
DS_GCC(PG:=DS_GPI.PG);

VGC_1(
	i_stUSG:= ,
	//i_stUSG:= US_GCC.IG,
	i_stDSG:= DS_GCC.IG , 
	i_xDis_DPIlk:= FALSE, 
	i_xEPS_OPN_OK:= , 
	i_xEPS_CLS_OK:= ,
	i_xPMPS_OK:= , 
	i_xExt_OK:= , 
	i_xOverrideMode:= , 
	i_xReset:= , 
	iq_stValve=>);

VGC_W(i_xExtILK_OK:= , 
	i_xOverrideMode:= ,
    iq_stValve=> );


]]></ST>
    </Implementation>
    <LineIds Name="PRG_VLS_WINDOW">
      <LineId Id="3" Count="27" />
      <LineId Id="127" Count="0" />
      <LineId Id="126" Count="0" />
      <LineId Id="31" Count="6" />
      <LineId Id="39" Count="20" />
      <LineId Id="63" Count="8" />
      <LineId Id="130" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="76" Count="8" />
      <LineId Id="129" Count="0" />
      <LineId Id="85" Count="0" />
      <LineId Id="128" Count="0" />
      <LineId Id="87" Count="13" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>